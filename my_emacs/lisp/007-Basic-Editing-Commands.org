* Basic Editing Commands

#+begin_src emacs-lisp
;;* 7 (info "(emacs)Basic") Editing Commands

(leuven--chapter leuven-load-chapter-7-basic "7 Basic Editing Commands"
#+end_src

** Inserting Text

#+begin_src emacs-lisp
;;** 7.1 (info "(emacs)Inserting Text")

  (leuven--section "7.1 (emacs)Inserting Text")

  ;; Enter characters by their code in octal (for `C-q NNN RET').
  (setq read-quoted-char-radix 8)       ; 16 for hexadecimal (for Unicode char)
#+end_src

** Moving Point Location

- ~M-r~ ::
     ~move-to-window-line~.

- ~M-g g~ ::
     *Jump to* the beginning of *line* number /N/ (~goto-line~).

#+begin_src emacs-lisp
;;** 7.2 (info "(emacs)Moving Point") Location

  (leuven--section "7.2 (emacs)Moving Point Location")

  ;; Don't add newlines to end of buffer when scrolling.
  (setq next-line-add-newlines nil)

  ;; Print the current buffer line number.
  (global-set-key (kbd "M-G") #'what-line)
#+end_src

Show line numbers temporarily -- just when you're going to a line number with
~goto-line~.

#+begin_src emacs-lisp
  (defun goto-line-with-feedback ()
    "Show line numbers temporarily, while prompting for the line number input"
    (interactive)
    (unwind-protect
        (progn
          (linum-mode 1)
          (goto-line (read-number "Goto line: ")))
      (linum-mode -1)))

  (global-set-key [remap goto-line] #'goto-line-with-feedback)
#+end_src

** Basic Undoing Changes

Shortcuts:

| Command | Emacs | Emacs-Leuven   |
|---------+-------+----------------|
| Undo    | C-/   | <f11>          |
| Redo    |       | C-S-z, <S-f11> |

#+begin_src emacs-lisp
;;** 7.4 (info "(emacs)Basic Undo")ing Changes

  (leuven--section "7.4 (emacs)Basic Undoing Changes")

  ;; Undo changes.
  (global-set-key (kbd "<f11>") #'undo)
#+end_src

#+begin_src emacs-lisp
  ;; Treat undo history as a tree.
  (with-eval-after-load "undo-tree-autoloads"

    ;; Enable Global-Undo-Tree mode.
    (global-undo-tree-mode 1))

  (with-eval-after-load "undo-tree"

    (with-eval-after-load "diminish-autoloads"
      (diminish 'undo-tree-mode))

    ;; Display times relative to current time in visualizer.
    (setq undo-tree-visualizer-relative-timestamps t)

    ;; Display time-stamps by default in undo-tree visualizer.
    (setq undo-tree-visualizer-timestamps t)
                                        ; Toggle time-stamps display using `t'.

    ;; Display diff by default in undo-tree visualizer.
    (setq undo-tree-visualizer-diff t)  ; Toggle the diff display using `d'.

    ;; Redo changes.
    (global-set-key (kbd "C-S-z")   #'undo-tree-redo)
    (global-set-key (kbd "<S-f11>") #'undo-tree-redo))
#+end_src

Visual feedback on some operations (~undo~, ~yank~, etc.).

#+begin_src emacs-lisp
  (with-eval-after-load "volatile-highlights-autoloads"
    (idle-require 'volatile-highlights)
    (with-eval-after-load "volatile-highlights"
      (volatile-highlights-mode 1)))
#+end_src

#+begin_src emacs-lisp
)                                       ; Chapter 7 ends here.
#+end_src

